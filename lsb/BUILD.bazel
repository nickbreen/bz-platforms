load(":defs.bzl", "lsb", "lsb_test", "lsb_toolchain")

package(default_visibility = ["//visibility:public"])

toolchain_type(
    name = "toolchain_type",
)

lsb_toolchain(
    name = "toolchain/redhat",
    src = "/etc/redhat-release",
    cmd = "cp $1 $2",
)

lsb_toolchain(
    name = "toolchain/systemd",
    src = "/etc/os-release",
    cmd = """. $1; echo ${!3} > $2""",
)

# Strictly check the OS's simple name
lsb(
    name = "name",
    out = "name.txt",
    prop = "NAME",
    target_compatible_with = select({
        "//dist:debian": [],
        "//dist:fedora": [],
        "//dist:ubuntu": [],
        "//dist:rocky": [],
        "//dist:centos": ["@platforms//:incompatible"],  # CentOS6's redhat-release only contains a pretty name
    }),
)

lsb_test(
    name = "test/name",
    data = ":name",
    expected = select({
        "//dist:debian": "Debian GNU/Linux",
        "//dist:fedora": "Fedora Linux",
        "//dist:ubuntu": "Ubuntu",
        "//dist:rocky": "Rocky Linux",
    }),
    target_compatible_with = select({
        "//dist:debian": [],
        "//dist:fedora": [],
        "//dist:ubuntu": [],
        "//dist:rocky": [],
        "//dist:centos": ["@platforms//:incompatible"],  # test will be SKIPPED for //dist:centos
    }),
)

# Loosely check the OS pretty-name
lsb(
    name = "pretty-name",
    out = "pretty-name.txt",
    prop = "PRETTY_NAME",
)

lsb_test(
    name = "test/pretty-name/debian",
    data = ":pretty-name",
    expected = "^Debian.*",
    regexp = True,
    target_compatible_with = ["//dist:debian"],
)

lsb_test(
    name = "test/pretty-name/fedora",
    data = ":pretty-name",
    expected = "^Fedora.*",
    regexp = True,
    target_compatible_with = ["//dist:fedora"],
)

lsb_test(
    name = "test/pretty-name/ubuntu",
    data = ":pretty-name",
    expected = "^Ubuntu.*",
    regexp = True,
    target_compatible_with = ["//dist:ubuntu"],
)

lsb_test(
    name = "test/pretty-name/rocky",
    data = ":pretty-name",
    expected = "^Rocky\\ Linux.*",
    regexp = True,
    target_compatible_with = ["//dist:rocky"],
)

lsb_test(
    name = "test/pretty-name/centos",
    data = ":pretty-name",
    expected = "^CentOS.*",
    regexp = True,
    target_compatible_with = ["//dist:centos"],
)
